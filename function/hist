#!/bin/zsh
emulate -L zsh -o noshortloops -o warncreateglobal -o extendedglob
zmodload zsh/{parameter,zutil}

typeset -gHA _HIST__OPTS=(
  -f 'force:       never ask for confirmation'
  -i 'interactive: always ask for confirmation'
  -s 'silent:      do not print anything'
)
typeset -gHA _HIST__ARGS=(
  d  'delete: remove from history'
  e  'edit:   remove from history, then modify & append as new'
  f  'fix:    remove from history, then load into buffer'
  g  'get:    load into buffer'
  l  'list:   look, but do not touch'
  r  'reload: re-initialize local history from file'
  u  'undo:   roll back to before last action in same session'
)

.hist.yes() {
  [[ -v opts[-f] ]] &&
    return 0

  print -n "$1"
  read -q '? [y/n] '
  local ret=$?
  print -l
  return ret
}

hist() {
  emulate -L zsh -o noshortloops -o warncreateglobal -o extendedglob

  local -A opts=()
  local REPLY

  zparseopts -D -A opts f i s v
  local act=$1 sel=$2

  if [[ $act != [${(kj::)_HIST__ARGS}] && ! -v opts[-s] ]]; then
    autoload -Uz .hist.help
    .hist.help
    return 1
  fi

  case $act in
    r)
      .hist.yes "Replace local history with contents of $HISTFILE?" &&
        builtin fc -p $HISTFILE $HISTSIZE $SAVEHIST
      return
      ;;
    u)
      if .hist.yes "Revert last 'hist' action & all entries since then?"; then
        builtin fc -P
        local ret=$?
        if (( ? == 0 )); then
          builtin fc -W
          [[ -v opts[-s] ]] ||
            print 'Revert successful. Previous history restored.'
        else
          [[ -v opts[-s] ]] ||
            print 'Unable to revert. No restore points available in this session.'
        fi
        return ret
      fi
      return
      ;;
  esac

  local -A entries=()
  local HISTORY_IGNORE
  [[ -z $sel ]] &&
    sel=0
  case $sel in
    0)
      local suffix=' #:hist:push-line:'
      HISTORY_IGNORE='*'$suffix
      entries=( "${(@kv)history[(R)$~HISTORY_IGNORE]%$suffix}" )
      ;;
    [[:digit:]]##)
      HISTORY_IGNORE=${(b)history[$sel]}
      entries[$sel]=$history[$sel]
      ;;
    -[[:digit:]]##)
      sel=${${(@k)history}[-sel]}
      HISTORY_IGNORE=${(b)history[$sel]}
      entries[$sel]=$history[$sel]
      ;;
    *)
      HISTORY_IGNORE=$sel
      entries=( "${(@kv)history[(R)$sel]}" )
      ;;
  esac

  if [[ ! -v opts[-s] ]]; then
    local key value
    for key in ${(@kn)entries}; do
      print "${(pl:$#HISTNO:)key}  ${(Z+C+)entries[$key]}"
    done
  fi

  if (( $#entries == 0 )); then
    [[ -v opts[-s] ]] ||
      print "No ${${${sel:#0}:+matching history entries}:-pushed lines} found."
    return 1
  fi

  if [[ $act != [gl] ]]; then
    local single_plural="${#entries} entries"; (( $#entries == 1 )) &&
      single_plural='1 entry'

    local msg="Delete the $single_plural above from history?"; [[ $act == e ]] &&
      msg="Merge the $single_plural above for editing?"

    if (( $#entries == 1 )) && [[ ! -v opts[-i] ]] || .hist.yes $msg; then
      builtin fc -W
      builtin fc -p $HISTFILE $HISTSIZE $SAVEHIST
      [[ $act != e && ! -v opts[-s] ]] &&
        print "$single_plural deleted."
    else
      [[ -v opts[-s] ]] ||
        print '0 entries deleted.'
      return 1
    fi
  fi

  case $act in
    e)
      autoload -Uz zed

      bindkey -M zed &> /dev/null || zed -b
      bindkey -M zed '^[^M' accept-line
      [[ -v opts[-s] ]] ||
        print -l '' '[Alt-Enter: Save]  [Ctrl-C: Cancel]' ''

      local file=$(mktemp)
      print -l ${(v)entries} > $file

      setopt NO_warncreateglobal
      {
        zed $file
      } always {
        if (( TRY_BLOCK_INTERRUPT > 0 )); then
          TRY_BLOCK_INTERRUPT=0
          {
            builtin fc -P
          } always {
            builtin fc -W
            print 'Deleted entries restored.'
          }
        else
          print -s $(< $file)
          builtin fc -W
          [[ -v opts[-s] ]] ||
            print -l '' 'New entry saved.'
        fi
      }
      ;;
    [fg])
      print -z ${(Fv)entries}
      ;;
  esac

  setopt NO_warncreateglobal
  reply=( "${(@kv)entries}" )
}

hist "$@"
