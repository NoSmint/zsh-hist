#!/bin/zsh
zmodload -F zsh/parameter p:functions

.hist.format() {
  emulate -L zsh
  setopt extendedglob warncreateglobal
  if zstyle -t :hist: expand-aliases; then
    setopt aliases
  else
    setopt NO_aliases
  fi

  REPLY="$1"
  {
    eval ".hist.tmp.() { $1 }" 2>/dev/null ||
        return
    [[ ${${(Z+C+)1}:#\;} == ${${(Z+C+)functions[.hist.tmp.]}:#\;} ]] ||
        return

    REPLY=''
    local body=$'\n'"$functions[.hist.tmp.]"
    local word chunk indent prefix
    for word in ${(Z+n+)body}; do
      chunk=${(M)body#*$word}
      body=${body#$chunk}
      prefix=${${chunk%$word}%%$'\n'$'\t'##( |)}
      indent=${${chunk#$prefix}%$word}
      REPLY+=$prefix${indent%$'\t'( |)}$word
    done
    REPLY=${REPLY#$'\n'}
  } always {
    [[ -v functions[.hist.tmp.] ]] &&
        unfunction .hist.tmp.
  }
}

.hist.format "$@"
