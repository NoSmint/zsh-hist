#!/bin/zsh
zmodload -F zsh/parameter p:functions

.hist.format() {
  emulate -L zsh; setopt extendedglob warncreateglobal
  if zstyle -t :hist: expand-aliases; then
    setopt aliases
  else
    setopt NO_aliases
  fi

  REPLY="$1"
  local -i ret=1
  zsh -nf <<< "$1" 2>/dev/null ||
    return
  {
    functions[.tmp.]="$1" 2>/dev/null ||
      return
    if [[ -n $functions[.tmp.] ]]; then
      REPLY=''
      local body=$'\n'$functions[.tmp.]
      local word chunk indent prefix
      for word in ${(Z+n+)functions[.tmp.]}; do
        chunk=${(M)body#*$word}
        body=${body#$chunk}
        prefix=${${chunk%$word}%%$'\n'$'\t'##( |)}
        indent=${${chunk#$prefix}%$word}
        REPLY+=$prefix${indent%$'\t'( |)}$word
      done
      REPLY=${REPLY#$'\n'}
      ret=0
    fi
  } always {
    [[ -v functions[.tmp.] ]] &&
      unfunction .tmp.
  }
  return ret
}

.hist.format "$@"
