#!/bin/zsh
zmodload -F zsh/parameter p:functions

.hist.format() {
  setopt localoptions NO_aliases
  REPLY="$1"
  local -i ret=1
  {
    functions[.tmp.]="$1" 2>/dev/null
    if [[ -n $functions[.tmp.] ]]; then
      local -a reply match mbegin mend
      reply=( "${(@)${(@)${(f)functions[.tmp.]}//(#s)$'\t'/}//$'\t'/  }" )
      REPLY=${(F)reply:/(#b)([[:space:]]##)\((*\)) (*)/$match[1]$match[2]$'\n'  $match[1]$match[3]}
      REPLY=${(F)${(f)REPLY}:/(#b)(*)(';'?)/$match[1]$'\n'${(M)match[1]##[[:space:]]##}$match[2]}
      reply=( ${(@)${(f)REPLY}[@]%%[[:space:]]##} )
      reply=( ${(@)reply[@]:/(#b)([^[:space:]]##) '() {'/$match[1]'() {'} )
      REPLY=${${(F)reply}//$'\n'[[:space:]]#(#b)(do|then)$'\n'/; $match[1]$'\n'}
      REPLY=${REPLY//$'\n'[[:space:]]#$'\n'/}
      ret=0
    fi
  } always {
    [[ -v functions[.tmp.] ]] &&
      unfunction .tmp.
  }
  return ret
}

.hist.format "$@"
